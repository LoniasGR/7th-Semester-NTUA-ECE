IN 10H

MVI B,01H ;B and C are 01F4H = 500D
MVI C,F4H ;

MVI D,00H ;D is the posiotion of the train on the LEDs

MVI E,00H ;E is the direction, if 0 means move left, if 1 or 3 don't move,
	   ; if 2 move right
MVI A,0DH
SIM
EI

MAIN:
LDA 2000H 	;check if the lsb switch is 1
ANI 01H	;if not do nothing	
CPI 01H	;
JNZ MAIN
JMP CHECK_EDGES

LED_ON:
MOV A,D
CMA
STA 3000H
DI
CALL DELB
EI
JMP MAIN

DIRECTION:	;
MOV A,D
CPI 00H	; if D is 0 its the first time the train is moving
JZ FIRST_TIME	; and starts at the right-most side
MOV A,E
CPI 00H	;else if E = direction is 0 move left, if it is 1 or 3
JZ LEFT	;don't move, else if it's 2 move right
CPI 01H
JZ LED_ON
CPI 02H
JZ RIGHT
CPI 03H
JZ LED_ON

FIRST_TIME:	;make D = 0000 0001
INR D		;
JMP LED_ON	;

LEFT:
MOV A,D	;move one step to the left and light the led
RLC 		;
MOV D,A	;
JMP LED_ON	;

RIGHT:		; same as before for right direction
MOV A,D
RRC
MOV D,A
JMP LED_ON

CHECK_EDGES:		;check if the train is on the left side
MOV A,D		
CPI 80H
JNZ CHECK_EDGES_2
MOV A,E		;if it is on left side, check the direction the train
CPI 00H		;is moving. if right then do nothing
JZ MAIN
JMP DIRECTION
 	
CHECK_EDGES_2:
MOV A,D		;same as before for right side
CPI 01H
JNZ DIRECTION
MOV A,E
CPI 02H
JZ MAIN
JMP DIRECTION

HLT

INTR_ROUTINE:
DI
PUSH PSW
PUSH B

ROUTINE_1:
LDA 2000H
ANI 01H
CPI 01H
JNZ ROUTINE_END

RIM		;
ANI 20H	;
CPI 20H	;
JZ ROUTINE_1	;
MVI C,32H	;Flowchart no.1 from 8085 from laboratory notes
MVI B,00H	;page 60
CALL DELB	;
RIM		;
ANI 20H	;
CPI 20H	;
JZ ROUTINE_1	;

INR E		;pick the direction depending on the total number of interruts
MOV A,E
CPI 04H
JZ RESET_E
JMP ROUTINE_END

RESET_E:		;if E>3 make it 0
MVI E,00H
JMP ROUTINE_END

ROUTINE_END:
POP B
POP PSW
EI
RET


END
